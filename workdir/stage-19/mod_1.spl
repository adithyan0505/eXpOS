//PROCESS MANAGER MODULE
alias funcNum R1;
alias PID R2;
if(funcNum == FREE_USER_AREA_PAGE) then	
	multipush(R1,R2);
	//call RELEASE_PAGE
	R1 = 2;
	R2 = [PROCESS_TABLE + PID * 16 + 11];
	call MOD_2;
	multipop(R1,R2);
	return;
endif;
if(funcNum == EXIT_PROCESS) then
	multipush(R1,R2);
	//call FREE_PAGE_TABLE
	R1 = 4;
	call MOD_1;
	//call FREE_USER_AREA_PAGE
	R1 = 2;
	call MOD_1; 
	multipop(R1,R2);
	[PROCESS_TABLE + PID * 16 + 4] = TERMINATED;
	return;
endif;
if(funcNum == FREE_PAGE_TABLE) then
	[PTBR+0] = -1;
	[PTBR+1] = "0000";
	[PTBR+2] = -1;
	[PTBR+3] = "0000";
	alias i R3;
	i = 2;
	while(i < 10) do
		if([PTBR + 2 * i] != -1) then
			multipush(R1,R2,R3);
			//call RELEASE_PAGE
			R1 = 2;
			R2 = [PTBR + 2 * i];
			call MOD_2;					
			multipop(R1,R2,R3);
			//invalidate entry
			[PTBR + 2 * i] = -1;
			[PTBR + 2 * i + 1] = "0000";
		endif;
		i = i + 1;	
	endwhile;
	
	//invalidate DISK MAP TABLE entries
	//release Heap disk blocks
	i = 2;
	alias blockNum R4;
	while(i <= 3) do
		blockNum = [DISK_MAP_TABLE + PID * 10 + i];
		if(blockNum != -1) then
			//invoke RELEASE_BLOCK of MEMORY_MANAGER_MODULE
			multipush(R1,R2,R3,R4);
			R3 = PID; R2 = blockNum; R1 = RELEASE_BLOCK;
			call MOD_2;
			multipop(R1,R2,R3,R4);
			[DISK_MAP_TABLE + PID * 10 + i] = -1;
		endif;
		i = i + 1;
	endwhile;
	//release Stack disk blocks
	i = 8;
	while(i <= 9) do
		blockNum = [DISK_MAP_TABLE + PID * 10 + i];
		if(blockNum != -1) then
			//invoke RELEASE_BLOCK of MEMORY_MANAGER_MODULE
			multipush(R1,R2,R3,R4);
			R3 = PID; R2 = blockNum; R1 = RELEASE_BLOCK;
			call MOD_2;
			multipop(R1,R2,R3,R4);
			[DISK_MAP_TABLE + PID * 10 + i] = -1;
		endif;
		i = i + 1;
	endwhile;
	//invalidate code entries
	i = 4;
	while(i <= 7) do
		[DISK_MAP_TABLE + PID * 10 + i] = -1;
		i = i + 1;
	endwhile;
	return;
endif;
